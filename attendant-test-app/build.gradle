apply plugin: 'com.android.application'
android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.localz.sdk.attendanttestapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 7
        versionName "0.6.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "pushsdk"

    productFlavors {
        fcm {
            dimension "pushsdk"
        }
        gcm {
            dimension "pushsdk"
        }
        pusher {
            dimension "pushsdk"
        }
        // TODO
//        pushy {
//            dimension "pushsdk"
//        }
        socket {
            dimension "pushsdk"
        }
    }
}

ext {
    supportLibVersion = '27.1.0'
    playServicesLibVersion = '11.8.0'
    spotzPushSdkVersion = '2.2.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'com.localz.sdk.attendant:attendant-sdk:0.6.1'
    implementation "com.google.android.gms:play-services-base:$playServicesLibVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesLibVersion"

    //Only required for FCM
    fcmImplementation "com.google.firebase:firebase-messaging:$playServicesLibVersion"
    fcmImplementation "com.google.android.gms:play-services-location:$playServicesLibVersion"
    fcmImplementation "com.localz.spotzpush.sdk:spotz-push-sdk-fcm:$spotzPushSdkVersion@aar"

    //Only required for SocketIO
    socketImplementation "com.localz.spotzpush.sdk:spotz-push-sdk-socket:$spotzPushSdkVersion@aar"
    socketImplementation ('io.socket:socket.io-client:0.8.3') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    //Only required for GCM
    gcmImplementation "com.google.android.gms:play-services-gcm:$playServicesLibVersion"
    gcmImplementation "com.localz.spotzpush.sdk:spotz-push-sdk-gcm:$spotzPushSdkVersion@aar"

    //Only required for Pusher
    pusherImplementation "com.localz.spotzpush.sdk:spotz-push-sdk-pusher:$spotzPushSdkVersion@aar"
    pusherImplementation 'org.slf4j:slf4j-api:1.7.5'

    // TODO - pushy

    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.http-client:google-http-client:1.23.0'
    implementation 'com.google.http-client:google-http-client-gson:1.23.0'

    implementation 'com.squareup.okhttp3:mockwebserver:3.9.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

// Only GCM and FCM need google-services
if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("Gcm") || getGradle().getStartParameter().getTaskRequests()
        .toString().contains("Fcm")){
    apply plugin: 'com.google.gms.google-services'
}
